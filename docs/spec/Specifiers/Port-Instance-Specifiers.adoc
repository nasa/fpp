=== Port Instance Specifiers

A *port instance specifier* specifies an instantiated 
<<Definitions_Port-Definitions,port>> as part
of a
<<Definitions_Component-Definitions,component definition>>.

==== Syntax

A port instance specifier is one of the following:

* _general-port-kind_ `port` <<Lexical-Elements_Identifiers,_identifier_>> `:` 
_[_
`[` <<Expressions,_expression_>> `]`
_]_
_port-instance-type_
_[_
`priority` <<Expressions,_expression_>>
_]_
_[_
_queue-full-behavior_
_]_

* _special-port-kind_ `port` <<Lexical-Elements_Identifiers,_identifier_>>

_general-port-kind_ is one of the following:

. `async` `input`

. `guarded` `input`

. `output`

. `sync` `input`

_port-instance-type_ is one of the following:

. <<Scoping-of-Names_Qualified-Identifiers,_qual-ident_>>

. `serial`

_queue-full-behavior_ is one of the following:

. `assert`

. `block`

. `drop`

_special-port-kind_ is one of the following:

. `command` `recv`

. `command` `reg`

. `command` `resp`

. `event`

. `param` `get`

. `param` `set`

. `telemetry`

. `text` `event`

. `time` `get`

The following port instances are *input port instances*:

* Any general port with `input` in its name.

* Special port `command recv`.

The others are *output port instances*.

==== Semantics

*General port instances:*

. The kind specifies the kind of the port instance.

. The identifier specifies the name of the port instance.

. The optional expression _e_ enclosed in brackets specifies the
number of port instances in the port instance array.
If no such expression appears, the default value is 1.
The type of _e_ must be <<Type-Checking_Type-Conversion,convertible to>>
<<Types_Internal-Types_Integer,_Integer_>>.
_e_ must
<<Evaluation,evaluate>> to a value _n_ after
<<Evaluation_Type-Conversion,conversion>> to _Integer_.
_n_ must be greater than zero.

. _port-instance-type_ specifies the type of the port instance.

.. If the type is a qualified identifier _q_, then _q_ must
<<Scoping-of-Names_Resolution-of-Qualified-Identifiers,refer>> to a
<<Definitions_Port-Definitions,port definition _D_>>.
If the kind is `async input`, then _D_
may not specify a return type.

.. If the type is `serial`, then the port instance is a *serial
port instance*.
A serial port instance does not specify a type.
It may be connected to a port of any type.
Serial data passes through the port.
The data may be converted to or
from a specific type at the other end of the connection.

. The optional expression _e_ appearing after the keyword
`priority` specifies a priority for the port instance.
The type of _e_ must be <<Type-Checking_Type-Conversion,convertible to>>
<<Types_Internal-Types_Integer,_Integer_>>.
The priority applies to the component's message queue and may appear only for 
`async input` ports.
The meaning of the priority value is operating system-dependent.

. The optional _queue-full-behavior_ specifies the behavior when a
message is received and the queue is full:

.. `assert` means that an assertion fails, terminating FSW.

.. `block` means that the sender is blocked until there is
space on the queue for the message.

.. `drop` means that the message is dropped.

+
This specifier is valid only for `async input` ports.
If no specifier appears, then the default behavior is `assert`.

*Special port instances:*
These port instances correspond to the "role ports" in the F Prime XML model.
Their types and other attributes are specified by F Prime.
The identifier specifies the name of the port.

There must be at most one of each kind of special port instance.

Each special port instance represents an implied use of a port
as shown in the table below.
These ports are defined by the F Prime framework.

|===
|Special port kind|Implied port use

|`command` `recv`
|`Fw.Cmd`

|`command` `reg`
|`Fw.CmdReg`

|`command` `resp`
|`Fw.CmdResponse`

|`event`
|`Fw.Log`

|`param` `get`
|`Fw.PrmGet`

|`param` `set`
|`Fw.PrmSet`

|`telemetry`
|`Fw.Tlm`

|`text` `event`
|`Fw.LogText`

|`time` `get`
|`Fw.Time`
|===

==== Examples

*General ports:*

[source,fpp]
----
@ Async input port of type Fw.Com
@ It has priority 10.
@ It drops input received when the queue is full.
async input port asyncComIn: Fw.Com priority 10 drop

@ Async input serial port
async input port serialIn: serial

@ Guarded input port of type Fw.Com
guarded input port guardedComIn: Fw.Com

@ Output port array of 10 Fw.Com ports
output port comOut: [10] Fw.Com

@ Sync input port of type Fw.Com
sync input port syncComIn: Fw.Com
----

*Special ports:*

[source,fpp]
----
@ A port for receiving commands from the command dispatcher
command recv port cmdIn

@ A port for sending command registration requests to the command dispatcher
command reg port cmdRegOut

@ A port for sending responses to the command dispatcher
command resp port cmdRespOut

@ A port for emitting events
event port eventOut

@ A port for emitting text events
text event port textEventOut

@ A port for getting parameter values from the parameter database
param get port paramGetOut

@ A port for sending parameter values to the parameter database
param set port paramSetOut

@ A port for emitting telemetry channels
telemetry port tlmOut

@ A port for getting the current time
time get port timeGetOut
----
