=== Event Specifiers

An *event specifier* specifies an event report as part of a
<<Definitions_Component-Definitions,component definition>>.

==== Syntax

`event`
<<Lexical-Elements_Identifiers,_identifier_>>
_[_
`(` <<Formal-Parameter-Lists,_param-list_>> `)`
_]_
`severity` _severity_
_[_
`id` <<Expressions,_expression_>>
_]_
`format` <<Expressions_String-Literals,_string-literal_>>
_[_
`throttle` _throttle_
_]_

_severity_ is one of the following:

* `activity` `high`
* `activity` `low`
* `command`
* `diagnostic`
* `fatal`
* `warning` `high`
* `warning` `low`

_throttle_ is defined as follows:

<<Expressions,_expression_>>
_[_
`every` <<Expressions,_expression_>>
_]_

==== Semantics

* The identifier names the event.

* _severity_ specifies the severity of the event.

* The parameter list specifies the event parameters.
If there are event parameters, each parameter must be
a <<Types_Displayable-Types,displayable type>>.
If there are no parameters, the list may be omitted.
`ref` may not appear in any of the parameters.

* The optional expression _e_ following `id` specifies the numeric
identifier for the event.
If _e_ is present, then the type of _e_ must be convertible to
<<Types_Internal-Types_Integer,_Integer_>>, and _e_ must evaluate
to a nonnegative integer.
If _e_ is not present, then the default identifier is either zero (for the
first
event appearing in a component) or the previous event identifier plus one.

* The string following `format` is a
<<Format-Strings,format string>> that formats the event for display on the
ground. The arguments to the format string are the values bound to the event
parameters. A numeric format is allowed for any
argument whose type is a <<Types_Internal-Types_Numeric-Types,numeric type>>.

* The optional _throttle_ syntax is for throttling events.
It has the following meaning:

** The expression _e_ following `throttle` specifies the *maximum throttle
count* for events with the specified numeric identifier.
The type of _e_ must be convertible to
<<Types_Internal-Types_Integer,_Integer_>> and must evaluate to an integer
in the range stem:[[0, 2^31)].
For each event identifier _I_ for which throttling is specified,
the framework maintains a *throttle count*.
The throttle count for identifier _I_
starts at zero and goes up by one with each event emitted with identifier _I_.
When the throttle count for identifier _I_ equals the maximum throttle count
for _I_, no more events
with identifier _I_ are emitted until the throttle count for identifier _I_ is 
reset.

** The optional expression _e_ following `every` specifies the *throttle
period*.
_e_ must satisfy the following rules:

*** The type of _e_ must be convertible to the
<<Types_Internal-Types_Anonymous-Struct-Types,
anonymous struct type>> _T =_ `{ seconds: U32, useconds: U32 }`.

*** After conversion to _T_, _e_ must evaluate to an
<<Values_Anonymous-Struct-Values,anonymous struct value>>
`{ seconds =` _s_ `:U32, useconds =` _u_ `:U32 }`.

*** _s_ must be in the range stem:[[0, 2^31)], _u_ must be in the range
stem:[[0,10^6)], and stem:[s + u] must be greater than zero.

+
+
If a throttle period stem:[p] is specified for event _I_, and the throttle 
count
for _I_ goes from zero to nonzero at time stem:[t], and a request
to emit event _I_ occurs at or after time stem:[t + p], then the event is 
emitted,
regardless of the throttle count for _I_, and the throttle count
for _I_ is automatically reset to zero.
If no throttle period is specified for event _I_, then the throttle count
for _I_ must be reset in some other way (typically this is done by command).

==== Examples

[source,fpp]
----
@ An array of 3 F64 values
array F64x3 = [3] F64

@ An enumeration of cases
enum Case { A, B, C }

@ Event 0
event Event0 \
  severity activity low \
  id 0x00 \
  format "Event 0 occurred"

@ Event 1
@ Sample output: "Event 1 occurred with argument 42"
event Event1(
  arg1: U32 @< Argument 1
) \
  severity activity high \
  id 0x01 \
  format "Event 1 occurred with argument {}"

@ Event 2
@ Sample output: "Saw value [ 0.001, 0.002, 0.003 ] for case A"
event Event2(
  case: Case @< The case
  value: F64x3 @< The value
) \
  severity warning low \
  id 0x02 \
  format "Saw value {} for case {}" \
  throttle 10

@ Event 3
@ Sample output: "Saw value [ 0.001, 0.002, 0.003 ] for case A"
event Event3(
  case: Case @< The case
  value: F64x3 @< The value
) \
  severity warning low \
  id 0x02 \
  format "Saw value {} for case {}" \
  throttle 10
  every {seconds=10}
----
