=== State Transition Specifiers

A *state transition specifier* is part of a
<<State-Machine-Behavior-Elements_State-Definitions,state definition>>.
It specifies a transition from the state in which it appears.

==== Syntax

`on` <<Lexical-Elements_Identifiers,_identifier_>>
_[_
`if` <<Lexical-Elements_Identifiers,_identifier_>>
_]_
_transition-or-do_

_transition-or-do_ is one of the following:

.  <<State-Machine-Behavior-Elements_Transition-Expressions,_transition-expression_>>

.  <<State-Machine-Behavior-Elements_Do-Expressions,_do-expression_>>

==== Semantics

. The identifier after the keyword `on` must
<<Definitions_State-Machine-Definitions_Semantics_Scoping-of-Names,refer>>
to a
<<State-Machine-Behavior-Elements_Signal-Definitions,signal definition>>.
It names the signal that causes the transition to occur.

. If present, the optional identifier after the keyword `if` must
<<Definitions_State-Machine-Definitions_Semantics_Scoping-of-Names,refer>>
to a
<<State-Machine-Behavior-Elements_Guard-Definitions,guard definition>>.
It specifies a guard for the transition.

. The first form of the _transition-or-do_ syntax specifies an *external
transition*, i.e., an optional list of actions and a target state.
The target state in the transition expression may be the same as the
enclosing state; in this case the transition is called a *self transition*.
When making a self transition, the state machine runs the code associated with
exiting and then re-entering the state.

. Second form of the _transition-or-do_ syntax specifies an
*internal transition*, i.e., a list of actions to take while remaining
in the same state.
When making an internal transition, the exit and re-entry code is not run.
The do expression must be valid.

==== Examples

[source,fpp]
----
state machine Device {

  action performStuff
  action powerHeater
  action powerSensor

  guard initComplete

  signal PowerOn
  signal RTI
  
  initial enter OFF

  state OFF {
    on PowerOn if initComplete do {
      powerHeater
      powerSensor
    } \
    enter ON
  }

  state ON {
    on RTI do { performStuff }
  }

}
----
