=== Topology Definitions

A *topology definition* defines an F Prime topology,
that is, a set of component instances and the connections
between their ports.
The connections are divided into named *connection graphs*.
The different graphs capture different aspects of FSW
function, such as commanding and telemetry.

One topology can be imported into another one.
The connections are merged graph by graph.

==== Syntax

`topology`
<<Lexical-Elements_Identifiers,_identifier_>>
`{` _topology-member-sequence_ `}`

_topology-member-sequence_ is an
<<Element-Sequences,element sequence>> in
which each element is a *topology member*,
and the terminating punctuation is a semicolon.
A topology member is one of the following:

* A <<Specifiers_Component-Instance-Specifiers,component
instance specifier>>

* A <<Specifiers_Connection-Graph-Specifiers,connection graph specifier>>

* A <<Specifiers_Telemetry-Packet-Set-Specifiers,telemetry packet set specifier>>

* A <<Specifiers_Topology-Import-Specifiers,topology import specifier>>

* A <<Specifiers_Port-Export-Specifiers,topology port export specifier>>

* An <<Specifiers_Include-Specifiers,include specifier>>

* A <<Definitions_Component-Instance-Definitions,component instance definition>>

==== Semantics

A topology definition _D_ must be resolvable to a topology _T_,
according to the following algorithm:

. Resolve _D_ to a
<<Definitions_Topology-Definitions_Semantics_Resolving-to-a-Partially-Numbered-Topology,
partially numbered topology>> _T'_.

. <<Definitions_Topology-Definitions_Semantics_Checking-Telemetry-Packet-Set-Specifiers,
Check telemetry packet set specifiers>> for _T'_.

. Apply
<<Definitions_Topology-Definitions_Semantics_Automatic-Numbering-of-Ports,
automatic numbering of ports>>
to _T'_.

===== Resolving to a Partially Numbered Topology

A *partially numbered topology* is a topology in which port number
assignments may or may not exist for each connection.
A port number assignment exists if
it is specified in the model source.

. For each endpoint _E_ (output or input) in each connection _C_
appearing in _D_,
if _E_ has a port number  expression _e_ in the FPP source, then
<<Evaluation,evaluate>> _e_,
<<Type-Checking_Type-Conversion,convert>> the result to a value _n_ of type
_Integer_, and assign the port number _n_ at that position.
Check that _n_ is in bounds for the port instance being numbered.

. Recursively resolve all the topologies directly imported into _D_.

. Construct the connection graphs of _T_ given by the
<<Specifiers_Connection-Graph-Specifiers,direct connection graphs>>
appearing in
_D_, after merging each set of connection graphs with the same name into
a single graph.
For example, the two connection graphs
+
[source,fpp]
----
connections C { a.b -> c.d }
----
+
and
+
[source,fpp]
----
connections C { e.f -> g.h }
----
+
are treated as the single graph
+
[source,fpp]
----
connections C { a.b -> c.d, e.f -> g.h }
----
+

. Compute the set _S_ of topologies transitively imported into _T_.

. Compute the set _U_ of topologies directly imported into _T_.

. For each topology _T'_ in _S_,
<<Specifiers_Topology-Import-Specifiers,import the instances>>
of _T'_ into _T_.

.. Note that only <<Specifiers_Component-Instance-Specifiers,component
instance specifiers>> instances from _T'_ will be imported into _T_.
<<Definitions_Component-Instance-Definitions,Component instance definitions>> inside _T'_
will be hidden (and cannot be directly connected to) inside _T_.

. Check that the connections discovered in step 3 are between port
instances present in _T_ or are <<Specifiers_Port-Export-Specifiers,exported ports>>
in of <<Specifiers_Topology-Import-Specifiers,imported topologies>> in _U_.

. For each topology _T'_ in _S_,
<<Specifiers_Topology-Import-Specifiers,import the connections>>
of _T'_ into _T_.

. Resolve
<<Specifiers_Connection-Graph-Specifiers,graph pattern specifiers>>,
adding connections to _T_.
Add only connections that are not already present in _T_.
For example, if a command pattern indicates a command
registration connection between two ports, and there is already
a command registration connection between those ports, then
do not add the connection.

.. Note: An exported port is also considered a connection. For example
if a topology _T_ exports a port _p_ from instance _I_, a direct connection to port _I_ `.` _p_ can not
be made to this port but rather only through the exported port _T_ `.` _p_.

===== Checking Telemetry Packet Set Specifiers

FPP checks telemetry packet set specifiers as follows:

. Check that no two telemetry packet set specifiers have
the same name.

. Check that each telemetry packet set specifier is
<<Specifiers_Telemetry-Packet-Set-Specifiers_Semantics,valid>>.

Note that a telemetry packet set specifier is not required.
If there is no specifier, then the topology has no packet definitions.
Note also that two or more telemetry packet set specifiers
are allowed.
Each one specifies a way of assigning the telemetry channels
of the topology to a set of packets.

===== Automatic Numbering of Ports

FPP automatically assigns port numbers as follows.

*Check output port numbers:*
At each output port _p_ of each component instance (or topology) _I_, check that

. The number of connections is in bounds for the
size of _I_ `.` _p_.

. There is no pair of connections stem:[c_1] and stem:[c_2]
at the same port number of _I_ `.` _p_.
For example, the following pair of connections is not allowed:
+
[source,fpp]
----
a.p[0] -> b.p
a.p[0] -> c.p
----

*Apply matched numbering:*
Assign matching numbers to matched pairs of ports.
For each instance _I_ in the topology:

. Let _C_ be the component type or a topology type of _I_.

.  For each
<<Specifiers_Port-Matching-Specifiers,port matching specifier>>
`match` stem:[p_1] `with` stem:[p_2] appearing in the definition of _C_:

.. For each of _i_ = 1 and _i_ = 2, check that no two distinct connections
at _I_ `.` stem:[p_i] have the same port number assigned to them.

.. Any connection that names _I_ `.` stem:[p_1] or _I_ `.` stem:[p_2]
is *match constrained*. If a match constrained connection is marked
`unmatched` then it is *unmatched*; otherwise it is *matched*.

..  For each matched connection stem:[c_1] with an endpoint at _I_ `.` stem:[p_1]:

... Let _I'_ be the component instance or imported topology at the other endpoint
of stem:[c_1].

... Check that there is one and only one matched connection
stem:[c_2] between _I'_ and _I_ `.` stem:[p_2].

.. Check that the connections stem:[c_2] computed in the previous
step are all the matched connections at _I_ `.` stem:[p_2].

.. For each pair stem:[(c_1,c_2)] computed in step c:

... If stem:[c_1] has a port number stem:[n_1] assigned at _I_ `.` stem:[p_1] and 
stem:[c_2] has a port number stem:[n_2] assigned at
_I_ `.` stem:[p_2], then check that stem:[n_1 = n_2].

... Otherwise if stem:[c_1] has a port number _n_ assigned at _I_ `.` stem:[p_1],

.... If no connection at _I_ `.` stem:[p_2] has port number _n_ assigned to it,
then assign _n_ to stem:[c_2] at _I_ `.` stem:[p_2].

.... Otherwise an error occurs.

... Otherwise if stem:[c_2] has a port number _n_ assigned at _I_ `.` stem:[p_2],

.... If no connection at _I_ `.` stem:[p_1] has port number _n_ assigned to it,
then assign _n_ to stem:[c_1] at _I_ `.` stem:[p_1].

.... Otherwise an error occurs.

.. Traverse the pairs stem:[(c_1,c_2)] computed in step c according to the
<<Definitions_Topology-Definitions_Semantics_Ordering-of-Connections,
order>> of the connections stem:[c_1], least to greatest.
For each pair stem:[(c_1,c_2)] that does not yet have assigned
port numbers, find the lowest available port number
and assign it at _I_ `.` stem:[p_1] and _I_ `.` stem:[p_2].
A port number _n_ is available if (a) _n_ is in bounds for _I_ `.` stem:[p_1]
and _I_ `.` stem:[p_2]; and (b)
_n_ is not already assigned to a connection at _I_ `.` stem:[p_1]; and (c)
_n_ is not already assigned to a connection at _I_ `.` stem:[p_2].
If no port number is available, then an error occurs.
Note that stem:[p_1] and stem:[p_2]
<<Specifiers_Port-Matching-Specifiers_Semantics,are required to have the
same size for their port arrays>>.

*Apply general numbering:*
Fill in any remaining port numbers.

. Traverse the connections
<<Definitions_Topology-Definitions_Semantics_Ordering-of-Connections,
in order>>, least to greatest.

. For each output endpoint _P_ in each connection _C_,
if no port number is already assigned, then assign the lowest available port
number at position _P_.


. For each input endpoint _P_ in each connection _C_, if no port number is
already assigned, then assign the port number zero.

See Example 4 in the <<Definitions_Topology-Definitions_Examples,Examples section>>.

===== Ordering of Connections

For purposes of port numbering, FPP orders connections as follows.

A connection endpoint in an FPP topology refers to either a
<<Definitions_Component-Instance-Definitions,component instance>> or an
<<Definitions_Topology-Definitions,topology definition>> as both have
<<Endpoint-Instance-Member-Identifiers,endpoint instance members>>.

A *connection endpoint* is _I_ `.` _p_ or _I_ `.` _p_ `[` _n_ `]`, where

. _I_ is one of the following

.. A <<Definitions_Component-Instance-Definitions,component instance definition>> that is
included in the topology via a <<Specifiers_Component-Instance-Specifiers,component instance specifier>>

.. A <<Definitions_Component-Instance-Definitions,component instance definition>> defined _inside_ _T_

... This is known as a "private component instance" as the endpoint cannot be connected to from
outside its enclosing topology.

.. A <<Definitions_Topology-Definitions,topology definition>> that is <<Specifiers_Topology-Import-Specifiers,imported>>
into _T_.

. And _p_ meets the following conditions:

.. If _I_ refers to a **component instance**, _p_ is an identifier that names a port instance specified in
the component definition associated with _I_.

.. If _I_ refers to a **topology definition**, _p_ identifiers an
<<Specifiers_Port-Export-Specifiers,exported port>> inside _I_.

. _n_ is an optional port number that is present if and only
if it appears in the model source.

When a connection endpoint _e_ has the form _I_ `.` _p_ `[` _n_ `]`,
we say that the endpoint *has source port number* _n_.

Each connection endpoint has a *fully qualified name*.
The fully qualified name is _Q_ `.` _p_, where _Q_ is the
<<Scoping-of-Names_Names-of-Definitions,fully qualified name>>
of the instance _I_.

FPP orders connection endpoints stem:[e_1] and stem:[e_2] as follows:

. If the fully qualified name of stem:[e_1] is lexically less
than (respectively greater than) the fully qualified name of
stem:[e_2], then stem:[e_1] is less than (respectively greater than) stem:[e_2].

. Otherwise if stem:[e_1] and stem:[e_2] have source port numbers
port numbers stem:[n_1] and stem:[n_2],
then the ordering of stem:[e_1]
and stem:[e_2] is the same as the numerical ordering of stem:[n_1]
and stem:[n_2].

. Otherwise stem:[e_1] and stem:[e_2] are equal in the ordering.

*Connections:* A *connection* is stem:[e_1] `pass:[->]`
stem:[e_2],
where stem:[e_1] and stem:[e_2] are the connection endpoints.
FPP orders connections stem:[c_1] and stem:[c_2] as follows:

. Let connection stem:[c_1] be stem:[e_1] `pass:[->]`
stem:[e'_1].

. Let connection stem:[c_2] be stem:[e_2] `pass:[->]`
stem:[e'_2].

. If stem:[e_1] is less than (respectively greater than)
stem:[e_2],
then stem:[c_1] is less than (respectively greater than) stem:[c_2].

. Otherwise if stem:[e'_1] is less than (respectively greater than)
stem:[e'_2], then stem:[c_1] is less than (respectively greater than)
stem:[c_2].

. Otherwise stem:[c_1] and stem:[c_2] are equal in the ordering.

==== Implied Uses

When generating a dictionary from a topology _T_, the analyzer may
treat the definition of _T_ as if it contained uses of
one or more <<Definitions_Alias-Type-Definitions_Framework-Definitions,
framework alias type definitions>> that are required by the dictionary.
These uses are called *implied uses*.

The management of these implied uses is left up to the FPP implementation.
The only requirement levied here is that, when generating a dictionary,
the FPP implementation must guarantee that all the framework definitions
required by the dictionary specification are available in the model.
Those framework definitions are specified
in the https://fprime.jpl.nasa.gov/latest/docs/reference/fpp-json-dict/[F Prime 
dictionary specification].

==== Examples

*Example 1.*

[source,fpp]
----
@ Command and data handling topology
topology CDH {

  # ----------------------------------------------------------------------
  # Public instances
  # ----------------------------------------------------------------------

  instance commandDispatcher
  instance commandSequencer
  instance engineeringRateGroup
  instance engineeringTelemetryLogger
  instance engineeringTelemetryConverter
  instance engineeringTelemetrySplitter
  instance eventLogger
  instance rateGroupDriver
  instance telemetryDatabase
  instance timeSource

  # ----------------------------------------------------------------------
  # Private instances
  # ----------------------------------------------------------------------

  private instance socketGroundInterface

  # ----------------------------------------------------------------------
  # Connection patterns
  # ----------------------------------------------------------------------

  command connections instance commandDispatcher
  event connections instance eventLogger
  time connections instance timeSource

  # ----------------------------------------------------------------------
  # Connection graphs
  # ----------------------------------------------------------------------

  connections CommandSequences {
    commandSequencer.comCmdOut -> commandDispatcher.comCmdIn
  }

  connections Downlink {
    eventLogger.comOut -> socketGroundInterface.comEventIn
    telemetryDatabase.comOut -> socketGroundInterface.comTlmIn
  }

  connections EngineeringTelemetry {
    commandDispatcher.tlmOut -> engineeringTelemetrySplitter.tlmIn
    commandSequencer.tlmOut -> telemetryDatabase.tlmIn
    engineeringRateGroup.tlmOut -> engineeringTelemetrySplitter.tlmIn
    engineeringTelmetryConverter.comTlmOut -> engineeringTelemetryLogger.comTlmIn
    engineeringTelemetrySplitter.tlmOut -> engineeringTelemetryConverter.tlmIn
    engineeringTelemetrySplitter.tlmOut -> telemetryDatabase.tlmIn
  }

  connections RateGroups {
    engineeringRateGroup.schedOut -> commandSequencer.schedIn
    engineeringRateGroup.schedOut -> telemetryDatabase.schedIn
    rateGroupDriver.cycleOut -> engineeringRateGroup.cycleIn
  }

  connections Uplink {
    socketGroundInterface.comCmdOut -> commandDispatcher.comCmdIn
  }

}
----

*Example 2.*

[source,fpp]
----
@ Attitude control topology
topology AttitudeControl {

  # ----------------------------------------------------------------------
  # Imported topologies
  # ----------------------------------------------------------------------

  import CDH

  # ----------------------------------------------------------------------
  # Public instances
  # ----------------------------------------------------------------------

  instance acsRateGroup
  instance attitudeControl
  ...

  # ----------------------------------------------------------------------
  # Private instances
  # ----------------------------------------------------------------------

  instance socketGroundInterface

  # ----------------------------------------------------------------------
  # Connection patterns
  # ----------------------------------------------------------------------

  command connections instance commandDispatcher
  event connections instance eventLogger
  time connections instance timeSource


  # ----------------------------------------------------------------------
  # Connection graphs
  # ----------------------------------------------------------------------

  connections AttitudeTelemetry {
    ...
  }

  connections Downlink {
    eventLogger.comOut -> socketGroundInterface.comEventIn
    telemetryDatabase.comOut -> socketGroundInterface.comTlmIn
  }

  connections EngineeringTelemetry {
    acsRateGroup.tlmOut -> engineeringTelemetrySplitter.tlmIn
    ...
  }

  connections RateGroups {
    acsRateGroup.schedOut -> attitudeControl.schedIn
  }

  connections Uplink {
    socketGroundInterface.comCmdOut -> commandDispatcher.comCmdIn
  }

}
----

*Example 3.*

[source,fpp]
----
@ Release topology
topology Release {

  # ----------------------------------------------------------------------
  # Imported topologies
  # ----------------------------------------------------------------------

  import AttitudeControl
  import CDH
  import Communication
  ...

  # Connecting sub topologies together
  connections CDH {
    Communication.uplink -> CDH.dataUp
    CDH.dataDown -> Communication.downlink
  }

}
----

*Example 4.*

Here is the topology that results from automatic numbering of ports
applied to topology `B` in the
<<Specifiers_Topology-Import-Specifiers_Example,example for topology import
specifiers>>:

[source,fpp]
----
topology B {

  instance a
  instance c
  instance d
  instance e
  instance f

  connections C1 {
    a.p1[0] -> c.p[0]
    a.p1[1] -> d.p[0]
  }

  connections C2 {
    a.p2[0] -> e.p[0]
  }

  connections C3 {
    a.p3[0] -> f.p[0]
  }

}
----
